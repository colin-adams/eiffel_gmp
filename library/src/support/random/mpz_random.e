note
	description: "COUNTABLE_SEQUENCE representing random numbers as MPZs."
	author: "Chris Saunders"
	date: "$Date: 2010-06-03 (Thurs, 03 June 2010) $"
	revision: "$Revision: 1.0.0.0 $"

class
	MPZ_RANDOM

inherit
	COUNTABLE_SEQUENCE [GMP_INTEGER]

create
	make

feature {NONE} -- Initialization

	make (n: NATURAL_32)
			-- Initialization for current.
			-- Numbers will be generated in the range in the range 0 to 2^`n'-1, inclusive.
		require
			n_positive: n > n.zero
		do
			create last_i_th
			create state
			maximum_value := n
		end

feature -- Access

	i_th (i: INTEGER_32): like item
			-- The `i'-th random number
		local
			j: INTEGER_32
		do
			if i < last_i then
				create state
				from
					j := 0
				until
					j = i
				loop
					{MPZ_FUNCTIONS}.mpz_urandomb (last_i_th.item, state.item, maximum_value)
					j := j + 1
				end
				last_i := j
			elseif i > last_i then
				from
					j := last_i
				until
					j = i
				loop
					{MPZ_FUNCTIONS}.mpz_urandomb (last_i_th.item, state.item, maximum_value)
					j := j + 1
				end
				last_i := j
			end
			create Result
			Result.copy (last_i_th)
		end

feature -- Measurement

	maximum_value: NATURAL_32
			-- 2^`maximum_value'-1 will be the largest number generated by `i_th'.

feature -- Element change

	set_seed (v: GMP_INTEGER)
			-- Set the seed value from `v'.
		do
			state.set_seed (v)
		end

	set_seed_natural_32 (v: NATURAL_32)
			-- Set the seed value from `v'.
		do
			state.set_seed_natural_32 (v)
		end

feature {NONE} -- Implementation

	last_i: INTEGER_32
		-- Last argument `i' from call to `i_th'.

	last_i_th: like item
		-- Last result of call to `i_th'.

	state: RANDOM_STATE
			-- Current state.

end
